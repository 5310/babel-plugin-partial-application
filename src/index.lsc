PACKAGE_NAME = 'babel-plugin-partial-application'
DEFAULT_PLACEHOLDER = '_'
ALTERNATE_PLACEHOLDER = '__'

makeIdentifierError (path, current) ->
  path.buildCodeFrameError(
    `Cannot use placeholder as an identifier.\n
     If you need to use '${current}' as a variable name:
     - set 'options.placeholder' to a different custom token
     - set 'options.useAlternatePlaceholder' for '__'
       ( a double underscore )\n
    See https://github.com/citycide/${PACKAGE_NAME}#caveats--limitations
    for more information.\n`
  )

makeDefaultArgError (path) ->
  path.buildCodeFrameError(
    'Default parameters are only supported in argument lists.'
  )

module.exports = ({ types: t }) ->
  hash = new Map()

  getPlaceholder (state) ->
    {
      file: { opts: { filename } },
      opts: { useAlternatePlaceholder }
    } = state

    let placeholder =
      hash.get(filename) or
      state.opts.placeholder or
      DEFAULT_PLACEHOLDER

    if placeholder == DEFAULT_PLACEHOLDER and useAlternatePlaceholder:
      now placeholder = ALTERNATE_PLACEHOLDER

    kind = typeof placeholder
    if kind != 'string':
      throw new TypeError(
        'Expected a string for `options.placeholder` ' +
        `and got ${kind}`
      )

    hash.set(filename, placeholder)
    placeholder

  setPlaceholder (state, token) ->
    if typeof token == 'string':
      hash.set(state.file.opts.filename, token)

  getUniqueName (path, name = 'a') ->
    path.scope.generateUidIdentifier(name)

  isPlaceholder (node, placeholder) ->
    t.isIdentifier(node, { name: placeholder })

  isSpreadPlaceholder (node, placeholder) ->
    t.isSpreadElement(node) and
    node.argument~isPlaceholder(placeholder)

  isPartialCaller (node, placeholder) ->
    t.isCallExpression(node) and
    node.arguments~hasPlaceholders(placeholder)

  isPluginRequire (node) ->
    if !node: return
    { callee, arguments: args } = node

    isRequire =
      t.isIdentifier(callee, { name: 'require' }) or (
        t.isMemberExpression(callee) and
        t.isIdentifier(callee.object, { name: 'require' })
      )

    isRequire and t.isStringLiteral(args[0], { value: PACKAGE_NAME })

  shouldHoist (node) ->
    if t.isCallExpression(node):
      return !node.callee?.object?.wasPlaceholder

    if t.isMemberExpression(node):
      return !node.object.wasPlaceholder

    !t.isLiteral(node) and [
      'Identifier'
      'SpreadElement'
      'FunctionExpression'
      'ArrowFunctionExpression'
    ].every(type => type != node.type)

  hasPlaceholders (nodes, placeholder) ->
    for elem node in nodes:
      if node~isPlaceholder(placeholder):
        return true

  makeWrapperFunction (params, returnValue) ->
    t.arrowFunctionExpression(
      params,
      t.blockStatement([
        t.returnStatement(returnValue)
      ])
    )

  findTopmostLink (path) ->
    let tail = path
    while tail.parentPath:
      parent = tail.parentPath
      isCalleeTail =
        parent.isCallExpression() and
        parent.get('callee') == tail
      hasChainedParent =
        parent.isMemberExpression() or
        parent.isBinaryExpression() or
        isCalleeTail

      if !hasChainedParent: break
      now tail = tail.parentPath

    tail

  checkImports (path, state) ->
    placeholder = state~getPlaceholder()
    { node: { specifiers, source } } = path
    if source.value == PACKAGE_NAME and specifiers.length:
      state~setPlaceholder(specifiers[0].local.name)
      return path.remove()

    for elem specifier in specifiers:
      if specifier.local.name == placeholder:
        throw makeIdentifierError(path, placeholder)

  checkDeclarations (path, state) ->
    placeholder = state~getPlaceholder()
    for elem { init, id } in path.node.declarations:
      if init~isPluginRequire() and t.isIdentifier(id):
        state~setPlaceholder(id.name)
        return path.remove()

      if id.name == placeholder:
        throw path~makeIdentifierError(placeholder)

  appliedChildVisitor = {
    CallExpression (childPath): void ->
      childPath.skip()

    Identifier (childPath): void ->
      { node, parent } = childPath

      if (
        childPath.key == 'property' or
        !node~isPlaceholder(this.placeholder)
      ): return

      let name = 'arg'
      if t.isMemberExpression(parent):
        now name = 'obj'
        parent.wasPlaceholder = true

      id = childPath~getUniqueName(name)
      if node.defaultParameter:
        t.assignmentPattern(id, node.defaultParameter)
          ~this.fn.params.push()
      else:
        this.fn.params.push(id)

      node.name = id.name
      node.wasPlaceholder = true

    SpreadElement (childPath): void ->
      { node: { argument } } = childPath

      if !argument~isPlaceholder(this.placeholder): return

      id = childPath~getUniqueName('spr')
      this.fn.params.push(t.restElement(id))
      childPath.replaceWith(t.spreadElement(id))
  }

  binaryChildVisitor = {
    Identifier (childPath): void ->

      if (
        !childPath.node~isPlaceholder(this.placeholder) or
        childPath.key == 'property'
      ): return

      id = childPath~getUniqueName('bin')
      this.tail.node.params.push(id)
      childPath.node.name = id.name
  }

  templateLiteralVisitor = {
    Identifier (childPath): void ->
      if !childPath.node~isPlaceholder(this.placeholder):
        return

      if childPath.key == 'property': return

      id = childPath~getUniqueName('exp')
      childPath.node.name = id.name

      if t.isArrowFunctionExpression(this.path):
        this.path.node.params.push(id)
        return

      makeWrapperFunction([id], this.path.node)
        ~this.path.replaceWith()

      this.path.stop()
  }

  assignmentVisitor = {
    AssignmentExpression (childPath) ->
      { node: { left, right }, listKey } = childPath

      if left~isPlaceholder(this.placeholder):
        if listKey != 'arguments':
          throw makeDefaultArgError(childPath)

        left.defaultParameter = right
        childPath.replaceWith(left)
        return

      links = []
      let link = left
      while link.object:
        links.push(link.property)
        now link = link.object

      if link~isPlaceholder(this.placeholder):
        if listKey != 'arguments':
          throw makeDefaultArgError(childPath)

        link._meta = { links, right }
        childPath.replaceWith(left)
  }

  return {
    visitor: {
      Program (path, state) {
        placeholder = state~getPlaceholder()
        path.traverse(assignmentVisitor, { placeholder })
      }

      ImportDeclaration (path, state) ->
        path~checkImports(state)

      VariableDeclaration (path, state) ->
        path~checkDeclarations(state)

      CallExpression (path, state) ->
        placeholder = state~getPlaceholder()
        { arguments: args } = path.node

        if args.length and hasPlaceholders(args, placeholder):
          fn = makeWrapperFunction([], path.node)
          path.traverse(appliedChildVisitor, { fn, placeholder })

          for elem arg, idx i in args:
            if arg~shouldHoist() and !arg~isPartialCaller(placeholder):
              id = path~getUniqueName('ref')
              ref = t.variableDeclaration('const', [
                t.variableDeclarator(id, arg)
              ])

              parent = path.getStatementParent()
              parent.insertBefore(ref)
              args[i] = id

          path.replaceWith(fn)

      MemberExpression (path, state) ->
        placeholder = state~getPlaceholder()
        { node: { object } } = path

        if !object~isPlaceholder(placeholder):
          return

        last = object._meta?.links?[0]
        param = object._meta?.right
        let pattern
        if last:
          name = path~getUniqueName('arg')
          now pattern = t.objectPattern([
            t.objectProperty(
              last,
              t.assignmentExpression('=', name, param)
            )
          ])

          if object._meta.links.length > 1:
            for idx i, elem link in object._meta.links:
              if i == 0: continue
              now pattern = t.objectPattern([
                t.objectProperty(link, pattern)
              ])

          top = path~findTopmostLink()
          if top.parentPath.container:
            makeWrapperFunction([pattern], top.parent)
              ~top.parentPath.replaceWith()
          else:
            top.parent.params.push(pattern)

          top.replaceWith(name)
          return

        let comparingPlaceholders = false
        binaryParent = path.findParent(
          p => t.isBinaryExpression(p)
        )

        ids = [path~getUniqueName('obj')]

        if binaryParent:
          { right } = binaryParent.node
          if right~isPlaceholder(placeholder):
            now comparingPlaceholders = true
          else:
            let target = right?.object or right?.callee?.object
            while target?.object or target?.callee:
              now target = target.object or target.callee.object

            if target~isPlaceholder(placeholder):
              now comparingPlaceholders = true
              id = path~getUniqueName('obj')
              ids.push(id)
              target.name = id.name

        object.name = ids[0].name

        tail = path~findTopmostLink()
        if tail.container:
          makeWrapperFunction(ids, tail.node)
            ~tail.replaceWith()
        else:
          for elem id in ids:
            tail.parent.params.push(id)

      SpreadElement (path, state) ->
        placeholder = state~getPlaceholder()
        { parent, parentPath, node: { argument } } = path

        if !argument~isPlaceholder(placeholder): return

        if path.listKey != 'arguments':
          throw path.parentPath.buildCodeFrameError(
            'Placeholder spread is only supported as a function argument.'
          )

        id = path~getUniqueName('spr')

        if path.container:
          path.replaceWith(t.spreadElement(id))
          makeWrapperFunction([t.restElement(id)], parent)
            ~parentPath.replaceWith()
        else:
          parent.params.push(t.restElement(id))
          path.node.argument = id

        parentPath.stop()

      BinaryExpression (path, state) ->
        placeholder = state~getPlaceholder()
        { node: { left, right } } = path
        nodes = [left, right].filter(v => isPlaceholder(v, placeholder))

        if !nodes.length: return

        let tail = path
        while t.isBinaryExpression(tail.parent):
          now tail = tail.parentPath

        if tail.container:
          makeWrapperFunction([], t.toExpression(tail.node))
            ~tail.replaceWith()
        else:
          now tail = tail.parentPath

        tail.traverse(binaryChildVisitor, { tail, placeholder })
        tail.stop()

      TemplateLiteral (path, state) ->
        placeholder = state~getPlaceholder()
        path.traverse(templateLiteralVisitor, { path, placeholder })
    }
  }
